#!/bin/zsh

# Constants
readonly program="$(basename "${0}")"

# Usage
function usage {
  echo "
    Rename image files to the date and time they were taken.

    Usage:
      ${program} <file...>

    Options:
      -h, --help   Show this help.
  " | sed -E 's/^ {4}//'

  exit "${1}"
}

# Checks
[[ "${1}" =~ ^(-h|--help)$ ]] && usage 0
[[ "${#}" -gt 0 ]] || usage 1

if ! hash 'exiftool' 2> /dev/null
then
  echo 'Missing "exiftool" in PATH. Install with:' >&2
  echo '  brew install exiftool' >&2
  exit 1
fi

# Main
for file in "${@}"
do
  filename="$(basename "${file}")"
  filedir="$(dirname "${file}")"
  fileextension="$([[ "${file}" == *.* ]] && tr '[:upper:]' '[:lower:]' <<< ".${file##*.}" || echo '')"

  pdatetime="$(exiftool -printFormat '$datetimeoriginal' "${file}" 2> /dev/null)"

  if [[ -z "${pdatetime}" ]]
  then
    echo "$(basename "${file}"): No date found. Skippingâ€¦" >&2
    continue
  fi

  pdate="$(cut -d' ' -f1 <<< "${pdatetime//:/-}")"
  ptime="$(cut -d' ' -f2 <<< "${pdatetime//:/.}")"

  newname="${pdate} ${ptime}${fileextension}"
  mv "${file}" "${filedir}/${newname}"
  echo "${filename}: Renamed to ${newname}"
done
