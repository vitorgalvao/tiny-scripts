#!/bin/zsh

readonly program="$(basename "${0}")"

# Usage
function usage {
  echo "
    Delete your GitHub forks where you do not have pending pull requests.

    Usage:
      ${program}

    Options:
      -h, --help   Show this help.
  " | sed -E 's/^ {4}//'
}

# Checks
if [[ "${1}" =~ ^(-h|--help)$ ]]
then
  usage
  exit 0
fi

if ! hash 'gh' 2> /dev/null
then
  echo 'Missing "gh" in PATH. Install with:' >&2
  echo '  brew install gh' >&2
  exit 1
fi

# Main
echo 'Delete fork ofâ€¦'

while IFS= read -r repo
do
  parent_repo="$(gh repo view "${repo}" --json parent --jq '.parent | "\(.owner.login)/\(.name)"')"

  # Skip if there are open pull requests in repo
  [[ "$(gh pr list --repo "${parent_repo}" --author '@me' --json title --jq 'length')" -eq 0 ]] || continue

  # Ask for delete confirmation
  echo -n "${parent_repo}"
  read -rq "? [y/N] "
  echo
  [[ "${REPLY}" == 'y' ]] || continue

  gh repo delete --yes "${repo}"
  echo
done < <(gh repo list --fork --json nameWithOwner --jq '.[].nameWithOwner')
